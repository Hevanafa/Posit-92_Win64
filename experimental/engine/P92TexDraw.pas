{ 
  Texture Draw unit
  Part of Posit-92 game engine
  By Hevanafa

  The term "texture" used in this unit is strictly in the context of CPU rendering
}

unit P92TexDraw;

{$Mode ObjFPC}
{$H-}  { Use ShortStrings }
{$J-}  { Don't allow assignments to typed consts }
{$B-}  { Enable boolean short-circuiting }
{$R-}  { Turn off range checks }
{$Q-}  { Turn off overflow checks }
{$Inline ON}

interface

uses P92AssetHandles;

procedure Spr(const texHandle: TTextureHandle; const x, y: smallint);

procedure SprTint(const texHandle: TTextureHandle; const x, y: smallint; const colour: longword);

{ This procedure is destructive }
procedure SprClear(const texHandle: TTextureHandle; const colour: longword);

procedure SprRegion(
  const texHandle: TTextureHandle;
  const srcX, srcY, srcW, srcH: smallint;
  const destX, destY: smallint);

procedure SprStretch(
  const texHandle: TTextureHandle;
  const destX, destY, destWidth, destHeight: smallint);

procedure SprRegionStretch(
  const texHandle: TTextureHandle;
  const srcX, srcY, srcWidth, srcHeight: smallint;
  const destX, destY, destWidth, destHeight: smallint);

procedure SprRegionTint(
  const texHandle: TTextureHandle;
  const srcX, srcY, srcW, srcH: smallint;
  const destX, destY: smallint;
  const colour: longword);

procedure SprFlipped(
  const texHandle: TTextureHandle;
  const x, y: smallint;
  const flip: smallint);

{ rotation is in radians }
procedure SprRotate(
  const texHandle: TTextureHandle;
  const cx, cy: smallint;
  const rotation: double);

procedure SprToDest(const src, dest: TTextureHandle; const x, y: smallint);

procedure SprRegionToDest(
  const src, dest: TTextureHandle;
  const srcX, srcY, srcW, srcH: smallint;
  const destX, destY: smallint);

procedure SprFlipInPlace(const texHandle: TTextureHandle; const flip: smallint);


implementation

uses
  P92Logger, P92Conversions,
  P92Tex, P92Maths,
  P92Panic, P92VGA;


procedure Spr(const texHandle: TTextureHandle; const x, y: smallint);
var
  texture: PSoftwareTex;
  startX, endX, startY, endY: smallint;
  rowBase, stride: longword;
  destRowBase, destStride: longword;

  px, py: smallint;
  { offset to the pixel data }
  offset: longword;
  alpha: byte;
begin
  if not IsTexSet(texHandle) then exit;

  texture := BorrowTexPtr(texHandle);

  { Handle clipping }
  startX := trunc(max(0, ClipX1 - x));
  endX := trunc(min(texture^.width - 1, ClipX2 - x));

  startY := trunc(max(0, ClipY1 - y));
  endY := trunc(min(texture^.height - 1, ClipY2 - y));

  if (startX > endX) or (startY > endY) then exit;

  stride := texture^.width * 4;
  destStride := VGAWidth * 4;

  for py := startY to endY do begin
    rowBase := py * stride;
    destRowBase := (y + py) * destStride;

    for px := startX to endX do begin
      offset := rowBase + px * 4;
      alpha := texture^.pixelData[offset + 3];

      if alpha < 255 then continue;

      PLongWord(@BorrowSurfacePtr^[destRowBase + (x + px) * 4])^ :=
        PLongWord(@texture^.pixelData[offset])^
    end;
  end;
end;

{
  Base version of Spr

  Made readable rather than optimised
}
procedure SprBase(const texHandle: longint; const x, y: smallint);
var
  texture: PSoftwareTex;
  px, py: smallint;
  { offset to the pixel data }
  offset: longword;
  alpha: byte;
  colour: longword;
begin
  if not IsTexSet(texHandle) then exit;

  texture := BorrowTexPtr(texHandle);

  for py:=0 to texture^.height - 1 do
    for px:=0 to texture^.width - 1 do begin
      if (x + px > ClipX2) or (x + px < ClipX1)
        or (y + py > ClipY2) or (y + py < ClipY1) then continue;

      offset := (px + py * texture^.width) * 4;

      alpha := texture^.pixelData[offset + 3];
      if alpha < 255 then continue;

      colour := UnsafeTexPGet(texture, px, py);
      UnsafePSet(x + px, y + py, colour)
    end;
end;

{ Copied from Spr }
procedure SprTint(const texHandle: TTextureHandle; const x, y: smallint; const colour: longword);
var
  texture: PSoftwareTex;
  startX, endX, startY, endY: smallint;
  rowBase, stride: longword;
  destRowBase, destStride: longword;

  px, py: smallint;
  { offset to the pixel data }
  offset: longword;
  alpha: byte;

  ABGR: longword;
begin
  if not IsTexSet(texHandle) then exit;

  texture := BorrowTexPtr(texHandle);

  { Handle clipping }

  startX := trunc(max(0, ClipX1 - x));
  endX := trunc(min(texture^.width - 1, ClipX2 - x));

  startY := trunc(max(0, ClipY1 - y));
  endY := trunc(min(texture^.height - 1, ClipY2 - y));

  if (startX > endX) or (startY > endY) then exit;

  { Render logic }

  stride := texture^.width * 4;
  destStride := VGAWidth * 4;

  ABGR := ARGBtoABGR(colour);

  for py := startY to endY do begin
    rowBase := py * stride;
    destRowBase := (y + py) * destStride;

    for px := startX to endX do begin
      offset := rowBase + px * 4;
      alpha := texture^.pixelData[offset + 3];

      if alpha < 255 then continue;

      PLongWord(@BorrowSurfacePtr^[destRowBase + (x + px) * 4])^ :=
        { PLongWord(@texture^.pixelData[offset])^ }
        ABGR;
    end;
  end;
end;

procedure SprClear(const texHandle: TTextureHandle; const colour: longword);
var
  texture: PSoftwareTex;
  px, py: smallint;
  ABGR: longword;
begin
  if not IsTexSet(texHandle) then exit;

  texture := BorrowTexPtr(texHandle);
  ABGR := ARGBtoABGR(colour);

  for py:=0 to texture^.height - 1 do
    for px:=0 to texture^.width - 1 do
      UnsafeTexPSet(texture, px, py, ABGR);
end;

{
  Base version of SprRegion

  Made readable rather than optimised
}
procedure SprRegionBase(
  const texHandle: TTextureHandle;
  const srcX, srcY, srcW, srcH: smallint;
  const destX, destY: smallint);
var
  texture: PSoftwareTex;
  a, b: smallint;
  sx, sy: smallint;
  srcPos: longword;
  alpha: byte;
  colour: longword;
begin
  if not IsTexSet(texHandle) then exit;

  texture := BorrowTexPtr(texHandle);

  for b:=0 to srcH - 1 do
  for a:=0 to srcW - 1 do begin
    if (destX + a > ClipX2) or (destX + a < ClipX1)
      or (destY + b > ClipY2) or (destY + b < ClipY1) then continue;

    sx := srcX + a;
    sy := srcY + b;
    srcPos := (sx + sy * texture^.width) * 4;

    alpha := texture^.pixelData[srcPos + 3];
    if alpha < 255 then continue;

    colour := UnsafeTexPGet(texture, sx, sy);
    UnsafePSet(destX + a, destY + b, colour);
  end;
end;

procedure SprRegion(
  const texHandle: TTextureHandle;
  const srcX, srcY, srcW, srcH: smallint;
  const destX, destY: smallint);
var
  texture: PSoftwareTex;
  surface: PByteArray;

  startX, endX, startY, endY: smallint;
  srcRowBase, texWidth4: longword;
  destRowBase, vgaWidth4: longword;

  a, b: smallint;
  srcOffset: longword;
  alpha: byte;
begin
  if not IsTexSet(texHandle) then exit;

  { Handle clipping }

  startX := 0;
  endX := srcW - 1;
  startY := 0;
  endY := srcH - 1;

  if destX + startX < ClipX1 then startX := ClipX1 - destX;
  if destX + endX > ClipX2 then endX := ClipX2 - destX;

  if destY + startY < ClipY1 then startY := ClipY1 - destY;
  if destY + endY > ClipY2 then endY := ClipY2 - destY;

  if (startX > endX) or (startY > endY) then exit;

  texture := BorrowTexPtr(texHandle);
  texWidth4 := texture^.width * 4;

  surface := BorrowSurfacePtr;
  vgaWidth4 := VGAWidth * 4;

  for b := startY to endY do begin
    srcRowBase := (srcY + b) * texWidth4 + srcX * 4;
    destRowBase := (destY + b) * vgaWidth4 + destX * 4;

    for a := startX to endX do begin
      srcOffset := srcRowBase + a * 4;

      alpha := texture^.pixelData[srcOffset + 3];
      if alpha < 255 then continue;

      PLongWord(@surface^[destRowBase + a * 4])^ :=
        PLongWord(@texture^.pixelData[srcOffset])^;
    end;
  end;
end;

{ Stretch a sprite with nearest neighbour scaling }

procedure SprStretch(
  const texHandle: TTextureHandle;
  const destX, destY, destWidth, destHeight: smallint
);
var
  sx, sy: smallint;
  dx, dy: smallint;
  srcPos: longword;
  texture: PSoftwareTex;
  alpha: byte;
  scaleX, scaleY: double;
  colour: longword;
begin
  if not IsTexSet(texHandle) then exit;
  texture := BorrowTexPtr(texHandle);

  scaleX := texture^.width / destWidth;
  scaleY := texture^.height / destHeight;

  for dy := 0 to destHeight - 1 do
  for dx := 0 to destWidth - 1 do begin
    if (destX + dx > ClipX2) or (destX + dx < ClipX1)
      or (destY + dy > ClipY2) or (destY + dy < ClipY1) then continue;

    sx := trunc(dx * scaleX);
    sy := trunc(dy * scaleY);

    srcPos := (sx + sy * texture^.width) * 4;
    alpha := texture^.pixelData[srcPos + 3];
    if alpha < 255 then continue;

    colour := UnsafeTexPGet(texture, sx, sy);
    UnsafePSet(dx + destX, dy + destY, colour);
  end;
end;

procedure SprRegionStretch(
  const texHandle: TTextureHandle;
  const srcX, srcY, srcWidth, srcHeight: smallint;
  const destX, destY, destWidth, destHeight: smallint);
var
  sx, sy: smallint;
  dx, dy: smallint;
  texture: PSoftwareTex;
  alpha: byte;
  scaleX, scaleY: double;
  colour: longword;
begin
  if not IsTexSet(texHandle) then exit;
  texture := BorrowTexPtr(texHandle);

  scaleX := srcWidth / destWidth;
  scaleY := srcHeight / destHeight;

  for dy := 0 to destHeight - 1 do
  for dx := 0 to destWidth - 1 do begin
    if (destX + dx > ClipX2) or (destX + dx < ClipX1)
      or (destY + dy > ClipY2) or (destY + dy < ClipY1) then continue;

    { Map destination pixel to source region }
    sx := srcX + trunc(dx * scaleX);
    sy := srcY + trunc(dy * scaleY);

    if (sx >= texture^.width) or (sx < 0)
      or (sy >= texture^.height) or (sy < 0) then continue;

    colour := UnsafeTexPGet(texture, sx, sy);

    alpha := colour shr 24;
    if alpha < 255 then continue;

    UnsafePSet(dx + destX, dy + destY, colour)
  end;
end;

procedure SprRegionTint(
  const texHandle: TTextureHandle;
  const srcX, srcY, srcW, srcH: smallint;
  const destX, destY: smallint;
  const colour: longword
);
var
  texture: PSoftwareTex;
  a, b: smallint;
  sx, sy: smallint;
  srcPos: longword;
  alpha: byte;
  ABGR: longword;
begin
  if not IsTexSet(texHandle) then exit;

  texture := BorrowTexPtr(texHandle);
  ABGR := ARGBtoABGR(colour);

  for b:=0 to srcH - 1 do
  for a:=0 to srcW - 1 do begin
    if (destX + a > ClipX2) or (destX + a < ClipX1)
      or (destY + b > ClipY2) or (destY + b < ClipY1) then continue;

    sx := srcX + a;
    sy := srcY + b;
    srcPos := (sx + sy * texture^.width) * 4;

    alpha := texture^.pixelData[srcPos + 3];
    if alpha < 255 then continue;

    UnsafePSet(destX + a, destY + b, ABGR);
  end;
end;

{ flip: use SprFlips enum }
procedure SprFlipped(
  const texHandle: TTextureHandle;
  const x, y: smallint;
  const flip: smallint
);
var
  sx, sy: smallint;
  dx, dy: smallint;
  srcPos: longword;
  texture: PSoftwareTex;
  alpha: byte;
  colour: longword;
begin
  if flip = SprFlipNone then begin
    Spr(texHandle, x, y);
    exit
  end;

  if not IsTexSet(texHandle) then exit;

  texture := BorrowTexPtr(texHandle);

  for sy := 0 to texture^.height - 1 do
  for sx := 0 to texture^.width - 1 do begin
    srcPos := (sx + sy * texture^.width) * 4;
    alpha := texture^.pixelData[srcPos + 3];

    if alpha < 255 then continue;

    dx := x + sx;
    dy := y + sy;

    case flip of
      SprFlipHorizontal:
        dx := x + texture^.width - sx - 1;
      SprFlipVertical:
        dy := y + texture^.height - sy - 1;
      else begin
        dx := x + texture^.width - sx - 1;
        dy := y + texture^.height - sy - 1;
      end
    end;

    if (dx > ClipX2) or (dx < ClipX1)
      or (dy > ClipY2) or (dy < ClipY1) then continue;

    colour := UnsafeTexPGet(texture, sx, sy);
    UnsafePSet(dx, dy, colour);
  end;
end;

procedure SprRotate(
  const texHandle: TTextureHandle;
  const cx, cy: smallint;
  const rotation: double
);
var
  sx, sy: double;
  dx, dy: smallint;
  srcPos: longword;
  srcX, srcY: smallint;
  texture: PSoftwareTex;

  alpha: byte;
  colour: longword;

  cosAngle, sinAngle: double;
  halfW, halfH: smallint;
  maxRadius: smallint;
begin
  if not IsTexSet(texHandle) then exit;
  texture := BorrowTexPtr(texHandle);

  { Negative for inverse transform }
  cosAngle := cos(-rotation);
  sinAngle := sin(-rotation);

  halfW := texture^.width div 2;
  halfH := texture^.height div 2;

  maxRadius := trunc(sqrt(halfW * halfW + halfH * halfH)) + 1;
  
  for dy := -maxRadius to maxRadius do
  for dx := -maxRadius to maxRadius do begin
    if (cx + dx < ClipX1) or (cx + dx > ClipX2)
      or (cy + dy < ClipY1) or (cy + dy > ClipY2) then continue;

    sx := dx * cosAngle - dy * sinAngle;
    sy := dx * sinAngle + dy * cosAngle;

    srcX := trunc(sx) + halfW;
    srcY := trunc(sy) + halfH;

    if (srcX < 0) or (srcX >= texture^.width)
      or (srcY < 0) or (srcY >= texture^.height) then continue;

    srcPos := (srcX + srcY * texture^.width) * 4;
    alpha := texture^.pixelData[srcPos + 3];
    if alpha < 255 then continue;

    colour := UnsafeTexPGet(texture, srcX, srcY);
    UnsafePSet(cx + dx, cy + dy, colour)
  end;
end;


procedure SprToDest(const src, dest: TTextureHandle; const x, y: smallint);
var
  srcTex, destTex: PSoftwareTex;
  startX, endX, startY, endY: word;
  a, b: smallint;
  srcOffset: longword;
  alpha: byte;
  colour: longword;
begin
  if not IsTexSet(src) or not IsTexSet(dest) then exit;

  srcTex := BorrowTexPtr(src);
  destTex := BorrowTexPtr(dest);

  startX := trunc(Max(0, -x));
  startY := trunc(Max(0, -y));
  endX := trunc(Min(srcTex^.width, destTex^.width - x));
  endY := trunc(Min(srcTex^.height, destTex^.height - y));

  for b:=startY to endY - 1 do
  for a:=startX to endX - 1 do begin
    srcOffset := (a + b * srcTex^.width) * 4;
    alpha := srcTex^.pixelData[srcOffset + 3];
    if alpha < 255 then continue;

    colour := UnsafeTexPGet(srcTex, a, b);
    UnsafeTexPSet(destTex, x + a, y + b, colour)
  end;
end;

procedure SprRegionToDest(
  const src, dest: TTextureHandle;
  const srcX, srcY, srcW, srcH: smallint;
  const destX, destY: smallint
);
var
  srcTex, destTex: PSoftwareTex;
  px, py: smallint;
  sx, sy: smallint;
  srcPos: longword;
  alpha: byte;
  colour: longword;
begin
  if not IsTexSet(src) then PanicHalt('SprRegionToDest: src handle is unset!');
  if not IsTexSet(dest) then PanicHalt('SprRegionToDest: dest handle is unset!');

  srcTex := BorrowTexPtr(src);
  destTex := BorrowTexPtr(dest);

  for py:=0 to srcH - 1 do
  { TODO: Hoist the Y bounds check and `sy` }
  for px:=0 to srcW - 1 do begin
    if (destX + px >= destTex^.width) or (destX + px < 0)
      or (destY + py >= destTex^.height) or (destY + py < 0) then continue;

    sx := srcX + px;
    sy := srcY + py;
    srcPos := (sx + sy * srcTex^.width) * 4;

    alpha := srcTex^.pixelData[srcPos + 3];
    if alpha < 255 then continue;

    colour := UnsafeTexPGet(srcTex, sx, sy);
    UnsafeTexPSet(destTex, destX + px, destY + py, colour);
  end;
end;

{ flip: Use SprFlipped enum }
procedure SprFlipInPlace(const texHandle: TTextureHandle; const flip: smallint);
var
  texture: PSoftwareTex;
  px, py: smallint;
  halfW, halfH: smallint;
  tempColour: longword;
  pos1, pos2: longint;
begin
  if flip = SprFlipNone then exit;
  if not IsTexSet(texHandle) then exit;

  texture := BorrowTexPtr(texHandle);

  { Horizontal flip }
  if (flip and SprFlipHorizontal) <> 0 then begin
    halfW := texture^.width div 2;

    for py:=0 to texture^.height - 1 do
    for px:=0 to halfW - 1 do begin
      pos1 := (px + py * texture^.width) * 4;
      pos2 := ((texture^.width - 1 - px) + py * texture^.width) * 4;

      { Swap RGBA }
      tempColour := PLongword(@texture^.pixelData[pos1])^;
      PLongword(@texture^.pixelData[pos1])^ := PLongword(@texture^.pixelData[pos2])^;
      PLongword(@texture^.pixelData[pos2])^ := tempColour
    end;
  end;

  { Vertical flip }
  if (flip and SprFlipVertical) <> 0 then begin
    halfH := texture^.height div 2;

    for py:=0 to halfH - 1 do
    for px:=0 to texture^.width - 1 do begin
      pos1 := (px + py * texture^.width) * 4;
      pos2 := (px + (texture^.height - 1 - py) * texture^.width) * 4;

      { Swap RGBA }
      tempColour := PLongword(@texture^.pixelData[pos1])^;
      PLongword(@texture^.pixelData[pos1])^ := PLongword(@texture^.pixelData[pos2])^;
      Plongword(@texture^.pixelData[pos2])^ := tempColour
    end;
  end;
end;


end.
