unit P92Core;

{$Mode ObjFPC}
{$H-}  { Use ShortStrings }
{$J-}  { Don't allow assignments to typed consts }

{DEFINE DebugEngineRunStates}

interface

uses P92AssetHandles;

{$IFDEF P92_WASM}
const
  Posit92Version = '0.6.4';

type
  TCallback = procedure;

  TP92AppConfig = record
    { default: "game" }
    CanvasID: string;

    { Default: "2d"
      Possible options: "2d" | "webgl" | "experimental-webgl" }
    Renderer: string;

    { default: 320 }
    BufferWidth: smallint;

    { default: 200 }
    BufferHeight: smallint;

    { default: 60
      0 makes it the same refresh rate as the monitor }
    TargetFPS: smallint;

    { default: true }
    LoadDefaultBMFont: boolean;
    { overridable, used together with `LoadDefaultBMFont` }
    DefaultBMFontPath: string;

    { default: true }
    LoadDefaultCursor: boolean;

    { default: true }
    EnableScreenshotHotkey: boolean;

    { default: false }
    EnableDrawFPS: boolean;

    { Callbacks }

    DrawLoading: TCallback;
  end;
{$ENDIF}

{$IFDEF P92_SDL2}
const
  Posit92Version = '0.3.1';

type
  TCallback = procedure;

  TP92AppConfig = record
    { SDL2 }

    WindowTitle: string;
    SDLScale: smallint;

    { Features }

    BufferWidth: smallint;
    BufferHeight: smallint;

    LoadDefaultBMFont: boolean;
    DefaultBMFontPath: string;

    TargetFPS: smallint;

    EnableScreenshotHotkey: boolean;
    { default: empty string, Downloads folder }
    ScreenshotsDir: string;

    LoadDefaultCursor: boolean;
    EnableDrawFPS: boolean;

    { Callbacks }

    OnPreload: TCallback;
    OnReady: TCallback;
    Update: TCallback;
    Draw: TCallback;
    OnCleanup: TCallback;
  end;
{$ENDIF}

{$IFDEF P92_WASM}
function IsBootFontLoaded: boolean; public name 'IsBootFontLoaded';
function GetBootFontHandle: TTextureHandle;
procedure SetBootFontHandle(const value: TTextureHandle);

function IsEngineReady: boolean; public name 'IsEngineReady';
procedure HostCallOnPreload; external 'env' name 'HostCallOnPreload';
procedure HostCallOnReady; external 'env' name 'HostCallOnReady';
{$ENDIF}

{$IFDEF P92_WASM}
procedure P92Boot; public name 'P92Boot';
procedure P92Update; public name 'P92Update';
procedure P92Draw; public name 'P92Draw';
procedure P92AfterDraw; public name 'P92AfterDraw';
{$ENDIF}

{$IFDEF P92_SDL2}
procedure P92Boot;
procedure P92Update;
procedure P92Draw;
procedure P92AfterDraw;
{$ENDIF}

function GetBootConfig: TP92AppConfig;

procedure PrintChar(const c: char; const x, y: smallint);
procedure Print(const txt: string; const x, y: smallint);

procedure PrintWrap(const txt: string; x, y, wrapWidth: smallint);

procedure PrintCharTint(const c: char; const x, y: smallint; const colour: longword);
procedure PrintTint(const txt: string; const x, y: smallint; const colour: longword);

function DefaultP92AppConfig: TP92AppConfig;
procedure P92Start(const appConfig: TP92AppConfig);

procedure DrawMouse;
procedure DrawFPS;


implementation

uses
{$ifdef P92_SDL2}
  SysUtils, SDL2, SDL2_Image,
  P92AssetRegistry, P92CoreSDL2,
  P92Fonts, P92Conversions, P92Logger,
  P92Keyboard, P92Mouse,
  P92TexDraw, P92TexRef,
  P92Strings, P92Timing, P92FPS, P92Sounds,
  P92Panic, P92VGA
{$endif}

{$ifdef P92_WASM}
  P92Fonts, P92AssetRegistry, P92WasmHeap, P92Conversions,
  P92FPS, P92Logger, P92Timing,
{$ifdef P92_ENABLE_SOUNDS}
  P92Sounds,
{$endif}
  P92Keyboard, P92Mouse,
  P92Tex, P92TexDraw, P92VGA, P92WasmHost, P92WasmMemMgr,
  P92InteropBuf, P92Loading
{$ifdef P92_WEBGL}
  , P92WebGL
{$endif}
{$endif}
{$ifdef P92_IMGUI}
  , P92IMGUI
{$endif}
  ;

{$IFDEF P92_WASM}
var
  texCursor: TTextureHandle;
{$ENDIF}

{$ifdef P92_SDL2}
var
  hwCursor: longint;
{$endif}

type
  TEngineRunStates = (
    ersBoot = 1,
    ersPreload = 2,
    ersReady = 3
  );

const
  DefaultCursorPath = 'assets/images/cursor.png';
  DefaultBMFontPath = 'assets/fonts/p92_sans_8_regular.txt';

  BootFontGlyphWidth = 8;
  BootFontGlyphHeight = 8;


var
  bootConfig: TP92AppConfig;
  engineRunState: TEngineRunStates;

  { Default boot font }
  BootFontHandle: TTextureHandle;

  { Used by screenshot }
  lastF2: boolean;
  screenshotHint: string;
  screenshotEndTick: double;

  fpsTop, fpsRight: smallint;


function GetBootConfig: TP92AppConfig;
begin
  GetBootConfig := bootConfig
end;

function GetBootFontHandle: TTextureHandle;
begin
  GetBootFontHandle := BootFontHandle
end;

procedure SetBootFontHandle(const value: TTextureHandle);
begin
  BootFontHandle := value
end;

procedure RequestBootFont;
begin
  SetBootFontHandle(
    RequestImage('assets/fonts/p92_boot.png'))
end;

function IsBootFontLoaded: boolean;
begin
  IsBootFontLoaded := (
    BorrowTexEntryPtr(BootFontHandle)^.status = AssetStatusReady);
end;

function IsEngineReady: boolean;
begin
  IsEngineReady := engineRunState = ersReady
end;

{$IFDEF P92_WASM}
procedure InitWasmRuntime;
var
  heapRegionStart: pointer;
begin
  JsInitWasmMemory(WasmMemorySize);

  InitVideoMem(Pointer(StackSize), bootConfig.BufferWidth, bootConfig.BufferHeight);

  heapRegionStart := pointer(StackSize + GetVideoMemSize);
  InitHeapRegion(heapRegionStart);

  InitHeapMgr;
  InitInteropBuffer;

  WriteInteropString(bootConfig.CanvasID);
  JsCreateCanvas(bootConfig.BufferWidth, bootConfig.BufferHeight);

  WriteInteropString(bootConfig.Renderer);
  JsInitCanvasCtx;

  JsSetTargetFPS(bootConfig.TargetFPS);
end;
{$ENDIF}

procedure P92Boot;
begin
{$ifdef P92_WASM}
  InitWasmRuntime;
{$endif}

  engineRunState := ersBoot;

{$IFDEF DebugEngineRunStates}
  WriteLog('ersBoot');
{$ENDIF}

{$ifdef P92_SDL2}
  InitVideoMem(
    GetMem(GetBootConfig.BufferWidth * GetBootConfig.BufferHeight * 4),
    bootConfig.BufferWidth, bootConfig.BufferHeight);

  TargetFPS := bootConfig.TargetFPS;
  FrameTime := 1000 div TargetFPS;
{$endif}

  InitDeltaTime;

  InitFPSCounter;
  fpsTop := 0;
  fpsRight := VGAWidth * 3 div 4;

  InitAssetRegistry;

{$ifdef P92_ENABLE_SOUNDS}
  InitSounds;
{$endif}
{$ifdef P92_WEBGL}
  SetupWebGLViewport;
  SetupWebGLShaders;
{$endif}
{$ifdef P92_SDL2}
  InitSDL;
  InitLogger;
{$endif}

  RequestBootFont;
end;

procedure InitPreloadState;
begin
{$ifdef P92_WASM}
  FitCanvas;
{$endif}

  engineRunState := ersPreload;

{$IFDEF DebugEngineRunStates}
  WriteLog('ersPreload');
{$ENDIF}

{$IFDEF P92_WASM}
  if bootConfig.LoadDefaultCursor then
    texCursor := RequestImage(DefaultCursorPath);
{$ENDIF}

{$IFDEF P92_SDL2}
  if bootConfig.LoadDefaultCursor then
    { imgCursor := LoadImage(DefaultCursorPath); }
    hwCursor := HwRequestImage(DefaultCursorPath)
  else
    hwCursor := 0;
{$ENDIF}

  if bootConfig.LoadDefaultBMFont then
    LoadDefaultBMFont;
  { else
    WriteLog('InitPreloadState: Skipped loading the default BMFont'); }

{$ifdef P92_WASM}
  HostCallOnPreload
{$endif}
end;

procedure InitReadyState;
begin
{$ifdef P92_WASM}
  FitCanvas;
{$endif}

  engineRunState := ersReady;

{$IFDEF DebugEngineRunStates}
  WriteLog('ersReady');
{$ENDIF}

{$IFDEF P92_IMGUI}
{$IFDEF P92_WASM}
  InitImmediateGUI(bootConfig.LoadDefaultBMFont);
{$ENDIF}
{$IFDEF P92_SDL2}
  InitImmediateGUI(bootConfig.LoadDefaultBMFont);
{$ENDIF}
{$ENDIF}

{$ifdef P92_WASM}
  HostCallOnReady
{$endif}
end;

{$IFDEF P92_SDL2}
function SHGetKnownFolderPath(
  rfid: PGUID;
  dwFlags: DWORD;
  hToken: THandle;
  out ppszPath: PWideChar
): HRESULT; stdcall; external 'shell32.dll';

procedure CoTaskMemFree(pv: Pointer); stdcall; external 'ole32.dll';

function GetDownloadsDir: AnsiString;
const
  GUIDDownloads: TGuid = '{374DE290-123F-4565-9164-39C4925E467B}';
var
  p: PWideChar;
begin
  GetDownloadsDir := '';

  if SHGetKnownFolderPath(@GUIDDownloads, 0, 0, p) = S_OK then begin
    GetDownloadsDir := UTF8Encode(UnicodeString(p));

    { The shell allocates the string & it must be freed manually }
    CoTaskMemFree(p);
  end;
end;

procedure SDL2TakeScreenshot;
var
  w, h: longint;
  screenshot: PSDL_Surface;
  filename, fullpath: AnsiString;
  msg: AnsiString;
begin
  { filename := 'test.png'; }
  filename := format('%s_%dx.png', [
    FormatDateTime('yyyy-mm-dd_hh-nn-ss', now),
    bootConfig.SDLScale
  ]);

  if bootConfig.ScreenshotsDir <> '' then
    fullpath := ConcatPaths([bootConfig.ScreenshotsDir, filename])
  else
    fullpath := ConcatPaths([GetDownloadsDir, filename]);

  SDL_GetRendererOutputSize(renderer, @w, @h);

  { The pixel format must match vgaTexture }
  screenshot := SDL_CreateRGBSurfaceWithFormat(0, w, h, 32, SDL_PIXELFORMAT_RGBA32);

  if screenshot = nil then begin
    WriteWarn('SDL2TakeScreenshot: Unable to create a screenshot');
    exit
  end;

  if SDL_RenderReadPixels(
    renderer, nil, SDL_PIXELFORMAT_RGBA32,
    screenshot^.pixels, screenshot^.pitch) = 0 then
  begin
    if IMG_SavePNG(screenshot, PAnsiChar(fullpath)) = 0 then begin
      msg := 'Saved as ' + fullpath;
      {
      SDL_ShowSimpleMessageBox(SDL_MESSAGEBOX_INFORMATION,
        'Screenshot', PAnsiChar(msg), window);
      }
      screenshotHint := msg;
      screenshotEndTick := GetTimer + 3.0;
    end;
  end else
    WriteWarn('SDL2TakeScreenshot: SavePNG failed: ' + SDL_GetError);

  SDL_FreeSurface(screenshot)
end;
{$ENDIF}

procedure TakeScreenshot;
{$IFDEF P92_WASM}
var
  filename: string;
  msg: string;
{$ENDIF}

begin

{$IFDEF P92_WASM}
  JsTakeScreenshot;

  filename := ReadInteropString;
  msg := 'Saved as ' + filename;

  screenshotHint := msg;
  screenshotEndTick := GetTimer + 3.0;

  writelog('TakeScreenshot: ' + msg);
{$ENDIF}

{$IFDEF P92_SDL2}
  SDL2TakeScreenshot
{$ENDIF}
end;

procedure P92Update;
begin
{$ifdef P92_WASM}
  if engineRunState = ersBoot then begin
    if AllAssetsReady then
      InitPreloadState;
    exit
  end

  else if engineRunState = ersPreload then begin
    if AllAssetsReady then
      InitReadyState;
    exit
  end

  else if engineRunState = ersReady then begin
    UpdateDeltaTime;
    IncrementFPS;
{$ifdef P92_IMGUI}
    ResetWidgetIndices;

    UpdateGUILastMouseButton;
    UpdateMouse;
    UpdateGUIMouseState;
{$else}
    UpdateMouse;
{$endif}
  end;
{$endif}

{$ifdef P92_SDL2}
  UpdateDeltaTime;
  IncrementFPS;
  HandleSDLEvents;
{$endif}

  if (screenshotHint <> '') and (GetTimer >= screenshotEndTick) then
    screenshotHint := '';

  if engineRunState = ersReady then begin
    if bootConfig.EnableScreenshotHotkey then begin
      if lastF2 <> isKeyDown(SC_F2) then begin
        lastF2 := isKeyDown(SC_F2);

        if lastF2 then
          TakeScreenshot;
      end;
    end;
  end;
end;

procedure DrawMouse;
begin
{$IFDEF P92_WASM}
  Spr(texCursor, GetMouseX, GetMouseY)
{$ENDIF}

{$IFDEF P92_SDL2}
  HwSpr(hwCursor, GetMouseX, GetMouseY)
{$ENDIF}
end;

procedure P92Draw;
begin
{$ifdef P92_WASM}
  if engineRunState = ersPreload then
    bootConfig.DrawLoading;
{$endif}
end;

procedure P92AfterDraw;
begin
{$IFDEF P92_IMGUI}
  ResetActiveWidget;
{$ENDIF}

{$IFDEF P92_WASM}
  if screenshotHint <> '' then
    PrintWrap(
      screenshotHint,
      0, VGAHeight - BootFontGlyphHeight,
      VGAWidth);

{$IFDEF P92_WEBGL}
  DrawMouse;

  if bootConfig.EnableDrawFPS then
    DrawFPS;

  VGAUpload;
  WebGLPresent;
{$ELSE}
  DrawMouse;

  if bootConfig.EnableDrawFPS then
    DrawFPS;

  VGAUpload;
  VGAPresent;
{$ENDIF}
{$ENDIF}

{$IFDEF P92_SDL2}
  if screenshotHint <> '' then
    PrintWrap(
      screenshotHint,
      0, VGAHeight - BootFontGlyphHeight * 2,
      VGAWidth);

  if bootConfig.EnableDrawFPS then
    DrawFPS;

  VGAUpload;

  { Begin hardware layer }
  DrawMouse;
  VGAPresent;
{$ENDIF}
end;

procedure PrintChar(const c: char; const x, y: smallint);
var
  row, col: smallint;
begin
  if not (ord(c) in [1..255]) then exit;

  row := ord(c) div 16;
  col := ord(c) mod 16;

  SprRegion(
    BootFontHandle,
    col * BootFontGlyphWidth,
    row * BootFontGlyphHeight,
    BootFontGlyphWidth,
    BootFontGlyphHeight,
    x, y)
end;

procedure Print(const txt: string; const x, y: smallint);
var
  c: char;
  left: smallint;
begin
  left := x;

  for c in txt do begin
    PrintChar(c, left, y);
    inc(left, BootFontGlyphWidth)
  end;
end;

procedure PrintWrap(const txt: string; x, y, wrapWidth: smallint);
var
  c: char;
  { relative to x }
  left: smallint;
begin
  left := 0;

  for c in txt do begin
    if c = #10 then begin
      left := 0;
      inc(y, BootFontGlyphWidth);
      continue;
    end;

    if c = #13 then continue;

    PrintChar(c, x + left, y);
    inc(left, BootFontGlyphWidth);

    if left >= wrapWidth then begin
      left := 0;
      inc(y, BootFontGlyphHeight);
    end;
  end;
end;

procedure PrintCharTint(const c: char; const x, y: smallint; const colour: longword);
var
  row, col: smallint;
begin
  if not (ord(c) in [1..255]) then exit;

  row := ord(c) div 16;
  col := ord(c) mod 16;

  SprRegionTint(
    BootFontHandle,
    col * BootFontGlyphWidth,
    row * BootFontGlyphHeight,
    BootFontGlyphWidth,
    BootFontGlyphHeight,
    x, y, colour)
end;

procedure PrintTint(const txt: string; const x, y: smallint; const colour: longword);
var
  c: char;
  left: smallint;
begin
  left := x;

  for c in txt do begin
    PrintCharTint(c, left, y, colour);
    inc(left, BootFontGlyphWidth)
  end;
end;

procedure DrawFPS;
begin
  if bootConfig.LoadDefaultBMFont then
    PrintDefault('FPS: ' + I32Str(GetLastFPS), fpsRight, fpsTop)
  else
    Print('FPS: ' + I32Str(GetLastFPS), fpsRight, fpsTop);

{$ifdef DEBUG_FPS}
  print('lastFPS: ' + i32str(lastFPS), VgaWidth - 160, 16);
  print('actualFPS: ' + i32str(actualFPS), VgaWidth - 160, 24);
  print('lastFPSTime: ' + f32str(lastFPSTime), VgaWidth - 160, 32);
{$endif}
end;

{$IFDEF P92_WASM}
function DefaultP92AppConfig: TP92AppConfig;
var
  newConfig: TP92AppConfig;
begin
  newConfig := default(TP92AppConfig);

  newConfig.CanvasID := 'game';
  newConfig.BufferWidth := 320;
  newConfig.BufferHeight := 200;

  newConfig.Renderer:= '2d';
  newConfig.TargetFPS := 60;

  newConfig.LoadDefaultBMFont := true;
  newConfig.DefaultBMFontPath := DefaultBMFontPath;

  newConfig.LoadDefaultCursor := true;
  newConfig.EnableScreenshotHotkey := true;
  newConfig.EnableDrawFPS := false;

  { Callbacks }

  newConfig.DrawLoading := @P92Loading.RenderLoadingScreen;

  DefaultP92AppConfig := newConfig;
end;

procedure P92Start(const appConfig: TP92AppConfig);
begin
  bootConfig := appConfig;
  P92Boot
end;
{$ENDIF}

{$IFDEF P92_SDL2}
procedure P92Cleanup;
begin
  { TODO: free both the imgCursor and the default font }
  { FreeTexture(imgCursor);
  FreeTexture(defaultFont.imgHandle); }

  freemem(BorrowSurfacePtr);
end;

procedure P92Shutdown;
begin
  CloseLogger;
  CleanupSDL
end;


function DefaultP92AppConfig: TP92AppConfig;
var
  newConfig: TP92AppConfig;
begin
  newConfig := default(TP92AppConfig);

  with newConfig do begin
    WindowTitle := 'Posit-92 + SDL2 on Windows';
    SDLScale := 2;

    BufferWidth := 320;
    BufferHeight := 200;

    LoadDefaultBMFont := true;
    DefaultBMFontPath := 'assets\fonts\p92_sans_8_regular.txt';

    TargetFPS := 60;

    EnableScreenshotHotkey := true;
    ScreenshotsDir := '';

    LoadDefaultCursor := true;
    EnableDrawFPS := false;
  end;

  DefaultP92AppConfig := newConfig
end;

procedure P92Start(const appConfig: TP92AppConfig);
begin
  bootConfig := appConfig;

  if not assigned(appConfig.Update) then
    PanicHalt('Update callback is required');
  if not assigned(appConfig.Draw) then
    PanicHalt('Draw callback is required');

  P92Boot;

  InitPreloadState;

  if Assigned(appConfig.OnPreload) then
    appConfig.OnPreload;

  InitReadyState;

  if Assigned(appConfig.OnReady) then
    appConfig.OnReady;

  done := false;

  { Game loop }
  lastFrameTime := SDL_GetTicks;

  while not done do begin
    frameTimeNow := SDL_GetTicks;
    elapsed := frameTimeNow - lastFrameTime;

    if elapsed >= FrameTime then begin
      P92Update;

      { User loop }
      appConfig.Update;
      appConfig.Draw;

      P92AfterDraw;

      lastFrameTime := frameTimeNow - (elapsed mod FrameTime) { Carry over extra time }
    end;

    SDL_Delay(1)
  end;

  if Assigned(appConfig.OnCleanup) then
    appConfig.OnCleanup;

  P92Cleanup;
  P92Shutdown
end;
{$ENDIF}


end.

