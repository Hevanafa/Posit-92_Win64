unit P92TexEffects;

{$Mode ObjFPC}
{$H-}  { Use ShortStrings }
{$J-}  { Don't allow assignments to typed consts }
{$Inline ON}

interface

uses P92AssetHandles;

{ colour: $AARRGGBB }
procedure SprOutline(const texHandle: TTextureHandle; const x, y: smallint; const colour: longword);

{ colour: $AARRGGBB }
procedure SprShadow(const texHandle: TTextureHandle; const x, y: smallint; const offsetX, offsetY: smallint; const colour: longword);

{ Replaces 1 colour of a texture in place
  Colour: $AARRGGBB }
procedure ReplaceColour(const texHandle: TTextureHandle; oldColour, newColour: longword);


implementation

uses P92Tex, P92TexDraw, P92VGA;

procedure SprOutline(const texHandle: TTextureHandle; const x, y: smallint; const colour: longword);
var
  a, b: smallint;
  texture: PSoftwareTex;
begin
  if not IsTexSet(texHandle) then exit;

  texture := BorrowTexPtr(texHandle);

  { Within sprite bounds }
  for b:=0 to texture^.height - 1 do
    for a:=0 to texture^.width - 1 do begin
      { Skip this solid pixel }
      if UnsafeTexGetAlpha(texture, a, b) > 0 then continue;

      { Check 4 neighbours }
      if (b - 1 >= 0) and (UnsafeTexGetAlpha(texture, a, b - 1) > 0)
        or (b + 1 < texture^.height) and (UnsafeTexGetAlpha(texture, a, b + 1) > 0)
        or (a - 1 >= 0) and (UnsafeTexGetAlpha(texture, a - 1, b) > 0)
        or (a + 1 < texture^.width) and (UnsafeTexGetAlpha(texture, a + 1, b) > 0) then
        pset(x + a, y + b, colour);
    end;
  
  { Padding area }
  { top & bottom }
  for a:=0 to texture^.width - 1 do begin
    if UnsafeTexGetAlpha(texture, a, 0) > 0 then
      pset(x + a, y - 1, colour);

    if UnsafeTexGetAlpha(texture, a, texture^.height - 1) > 0 then
      pset(x + a, y + texture^.height, colour);
  end;

  { left & right }
  for b:=0 to texture^.height - 1 do begin
    if UnsafeTexGetAlpha(texture, 0, b) > 0 then
      pset(x - 1, y + b, colour);

    if UnsafeTexGetAlpha(texture, texture^.width - 1, b) > 0 then
      pset(x + texture^.width, y + b, colour);
  end;

  spr(texHandle, x, y)
end;


procedure SprShadow(const texHandle: TTextureHandle; const x, y: smallint; const offsetX, offsetY: smallint; const colour: longword);
var
  a, b: smallint;
  destX, destY: smallint;
  texture: PSoftwareTex;
  alpha: byte;
begin
  if not IsTexSet(texHandle) then exit;

  texture := BorrowTexPtr(texHandle);

  alpha := colour shr 24 and $FF;
  if alpha = 0 then exit;

  for b:=0 to texture^.height - 1 do
  for a:=0 to texture^.width - 1 do begin
    if UnsafeTexGetAlpha(texture, a, b) < 255 then continue;

    destX := x + a + offsetX;
    destY := y + b + offsetY;

    if (destX < clipX1) or (destX > clipX2)
      or (destY < clipY1) or (destY > clipY2) then continue;

    if alpha = 255 then
      UnsafePSet(destX, destY, colour)
    else
      UnsafePSetBlend(destX, destY, colour);
  end;
  
  spr(texHandle, x, y)
end;

procedure ReplaceColour(const texHandle: TTextureHandle; oldColour, newColour: longword);
var
  a, b: word;
  texture: PSoftwareTex;
begin
  if not IsTexSet(texHandle) then exit;

  texture := BorrowTexPtr(texHandle);

  oldColour := ARGBtoABGR(oldColour);
  newColour := ARGBtoABGR(newColour);

  for b:=0 to texture^.height - 1 do
    for a:=0 to texture^.width - 1 do
      if UnsafeTexPGet(texture, a, b) = oldColour then
        UnsafeTexPSet(texture, a, b, newColour);
end;

end.
