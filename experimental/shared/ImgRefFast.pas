{ 
  ImgRefFast unit - Part of Posit-92 game engine
  Hevanafa
  
  Based on SprFast unit
}

unit ImgRefFast;

{$Mode ObjFPC}
{$H+}{$J-}
{$B-}  { Enable boolean short-circuiting }
{$R-}  { Turn off range checks }
{$Q-}  { Turn off overflow checks }

interface

{ spr with TImageRef }
procedure spr(const imgHandle: longint; const x, y: smallint);

procedure sprClear(const imgHandle: longint; const colour: longword);

procedure sprRegion(
  const imgHandle: longint;
  const srcX, srcY, srcW, srcH: smallint;
  const destX, destY: smallint);

procedure sprStretch(const imgHandle: longint; const destX, destY, destWidth, destHeight: smallint);

procedure sprRegionStretch(
  const imgHandle: longint;
  const srcX, srcY, srcWidth, srcHeight: smallint;
  const destX, destY, destWidth, destHeight: smallint);

procedure sprRegionSolid(
  const imgHandle: longint;
  const srcX, srcY, srcW, srcH: smallint;
  const destX, destY: smallint;
  const colour: longword);

procedure sprFlip(const imgHandle: longint; const x, y: smallint; const flip: smallint);

{ rotation is in radians }
procedure sprRotate(const imgHandle: longint; const cx, cy: smallint; const rotation: double);


procedure sprToDest(const src, dest: longint; const x, y: smallint);
procedure sprRegionToDest(
  const src, dest: longint;
  const srcX, srcY, srcW, srcH: smallint;
  const destX, destY: smallint);
procedure sprFlipInPlace(const imgHandle: longint; const flip: smallint);


implementation

uses Logger, Conv, ImgRef, Maths, Panic, VGA;

procedure spr(const imgHandle: longint; const x, y: smallint);
var
  image: PImageRef;
  px, py: smallint;
  offset: longword;
  { data: PByte; }
  a: byte;
  colour: longword;
begin
  if not isImageSet(imgHandle) then exit;

  image := getImagePtr(imgHandle);

  if image^.allocSize = 0 then
    panicHalt('imgHandle ' + i32str(imgHandle) + ' allocSize is 0!');
  
  for py:=0 to image^.height - 1 do
  for px:=0 to image^.width - 1 do begin
    if (x + px > clipX2) or (x + px < clipX1)
      or (y + py > clipY2) or (y + py < clipY1) then continue;

    { offset to ImageData buffer }
    offset := (px + py * image^.width) * 4;

    a := image^.dataPtr[offset + 3];
    if a < 255 then continue;

    colour := unsafeSprPget(image, px, py);
    unsafePset(x + px, y + py, colour)
  end;
end;

procedure sprClear(const imgHandle: longint; const colour: longword);
var
  image: PImageRef;
  px, py: smallint;
begin
  if not isImageSet(imgHandle) then exit;

  image := getImagePtr(imgHandle);

  { fillchar(image^.dataPtr, image^.width * image^.height * 4, 0); }
  for py:=0 to image^.height - 1 do
  for px:=0 to image^.width - 1 do
    unsafeSprPset(image, px, py, colour);
end;

procedure sprRegion(
  const imgHandle: longint;
  const srcX, srcY, srcW, srcH: smallint;
  const destX, destY: smallint);
var
  image: PImageRef;
  a, b: smallint;
  sx, sy: smallint;
  srcPos: longword;
  alpha: byte;
  colour: longword;
begin
  if not isImageSet(imgHandle) then exit;

  image := getImagePtr(imgHandle);

  for b:=0 to srcH - 1 do
  for a:=0 to srcW - 1 do begin
    if (destX + a > clipX2) or (destX + a < clipX1)
      or (destY + b > clipY2) or (destY + b < clipY1) then continue;

    sx := srcX + a;
    sy := srcY + b;
    srcPos := (sx + sy * image^.width) * 4;

    alpha := image^.dataPtr[srcPos + 3];
    if alpha < 255 then continue;

    colour := unsafeSprPget(image, sx, sy);
    unsafePset(destX + a, destY + b, colour);
  end;
end;

{ Stretch a sprite with nearest neighbour scaling }
procedure sprStretch(const imgHandle: longint; const destX, destY, destWidth, destHeight: smallint);
var
  sx, sy: smallint;
  dx, dy: smallint;
  srcPos: longword;
  image: PImageRef;
  alpha: byte;
  scaleX, scaleY: double;
  colour: longword;
begin
  if not isImageSet(imgHandle) then exit;
  image := getImagePtr(imgHandle);

  scaleX := image^.width / destWidth;
  scaleY := image^.height / destHeight;

  for dy := 0 to destHeight - 1 do
  for dx := 0 to destWidth - 1 do begin
    if (destX + dx > clipX2) or (destX + dx < clipX1)
      or (destY + dy > clipY2) or (destY + dy < clipY1) then continue;

    sx := trunc(dx * scaleX);
    sy := trunc(dy * scaleY);

    srcPos := (sx + sy * image^.width) * 4;
    alpha := image^.dataPtr[srcPos + 3];
    if alpha < 255 then continue;

    colour := unsafeSprPget(image, sx, sy);
    unsafePset(dx + destX, dy + destY, colour);
  end;
end;

procedure sprRegionStretch(
  const imgHandle: longint;
  const srcX, srcY, srcWidth, srcHeight: smallint;
  const destX, destY, destWidth, destHeight: smallint);
var
  sx, sy: smallint;
  dx, dy: smallint;
  image: PImageRef;
  alpha: byte;
  scaleX, scaleY: double;
  colour: longword;
begin
  if not isImageSet(imgHandle) then exit;
  image := getImagePtr(imgHandle);

  scaleX := srcWidth / destWidth;
  scaleY := srcHeight / destHeight;

  for dy := 0 to destHeight - 1 do
  for dx := 0 to destWidth - 1 do begin
    if (destX + dx > clipX2) or (destX + dx < clipX1)
      or (destY + dy > clipY2) or (destY + dy < clipY1) then continue;

    { Map destination pixel to source region }
    sx := srcX + trunc(dx * scaleX);
    sy := srcY + trunc(dy * scaleY);

    if (sx >= image^.width) or (sx < 0)
      or (sy >= image^.height) or (sy < 0) then continue;

    colour := unsafeSprPget(image, sx, sy);
    alpha := colour shr 24;
    if alpha < 255 then continue;

    unsafePset(dx + destX, dy + destY, colour)
  end;
end;

procedure sprRegionSolid(
  const imgHandle: longint;
  const srcX, srcY, srcW, srcH: smallint;
  const destX, destY: smallint;
  const colour: longword);
var
  image: PImageRef;
  a, b: smallint;
  sx, sy: smallint;
  srcPos: longword;
  alpha: byte;
begin
  if not isImageSet(imgHandle) then exit;

  image := getImagePtr(imgHandle);

  for b:=0 to srcH - 1 do
  for a:=0 to srcW - 1 do begin
    if (destX + a > clipX2) or (destX + a < clipX1)
      or (destY + b > clipY2) or (destY + b < clipY1) then continue;

    sx := srcX + a;
    sy := srcY + b;
    srcPos := (sx + sy * image^.width) * 4;

    alpha := image^.dataPtr[srcPos + 3];
    if alpha < 255 then continue;

    { colour := unsafeSprPget(image, sx, sy); }
    unsafePset(destX + a, destY + b, colour);
  end;
end;

{ flip: use SprFlips enum }
procedure sprFlip(const imgHandle: longint; const x, y: smallint; const flip: smallint);
var
  sx, sy: smallint;
  dx, dy: smallint;
  srcPos: longword;
  image: PImageRef;
  alpha: byte;
  colour: longword;
begin
  if flip = SprFlipNone then begin
    spr(imgHandle, x, y);
    exit
  end;

  if not isImageSet(imgHandle) then exit;

  image := getImagePtr(imgHandle);

  for sy := 0 to image^.height - 1 do
  for sx := 0 to image^.width - 1 do begin
    srcPos := (sx + sy * image^.width) * 4;
    alpha := image^.dataPtr[srcPos + 3];
    if alpha < 255 then continue;

    dx := x + sx;
    dy := y + sy;

    case flip of
      SprFlipHorizontal:
        dx := x + image^.width - sx - 1;
      SprFlipVertical:
        dy := y + image^.height - sy - 1;
      else begin
        dx := x + image^.width - sx - 1;
        dy := y + image^.height - sy - 1;
      end
    end;

    if (dx > clipX2) or (dx < clipX1)
      or (dy > clipY2) or (dy < clipY1) then continue;

    colour := unsafeSprPget(image, sx, sy);
    unsafePset(dx, dy, colour);
  end;
end;

procedure sprRotate(const imgHandle: longint; const cx, cy: smallint; const rotation: double);
var
  sx, sy: double;
  dx, dy: smallint;
  srcPos: longword;
  srcX, srcY: smallint;
  image: PImageRef;

  alpha: byte;
  colour: longword;

  cosAngle, sinAngle: double;
  halfW, halfH: smallint;
  maxRadius: smallint;
begin
  if not isImageSet(imgHandle) then exit;
  image := getImagePtr(imgHandle);

  { Negative for inverse transform }
  cosAngle := cos(-rotation);
  sinAngle := sin(-rotation);

  halfW := image^.width div 2;
  halfH := image^.height div 2;

  maxRadius := trunc(sqrt(halfW * halfW + halfH * halfH)) + 1;
  
  for dy := -maxRadius to maxRadius do
  for dx := -maxRadius to maxRadius do begin
    if (cx + dx < clipX1) or (cx + dx > clipX2)
      or (cy + dy < clipY1) or (cy + dy > clipY2) then continue;

    sx := dx * cosAngle - dy * sinAngle;
    sy := dx * sinAngle + dy * cosAngle;

    srcX := trunc(sx) + halfW;
    srcY := trunc(sy) + halfH;

    if (srcX < 0) or (srcX >= image^.width)
      or (srcY < 0) or (srcY >= image^.height) then continue;

    srcPos := (srcX + srcY * image^.width) * 4;
    alpha := image^.dataPtr[srcPos + 3];
    if alpha < 255 then continue;

    colour := unsafeSprPget(image, srcX, srcY);
    unsafePset(cx + dx, cy + dy, colour)
  end;
end;


procedure sprToDest(const src, dest: longint; const x, y: smallint);
var
  srcImage, destImage: PImageRef;
  startX, endX, startY, endY: word;
  a, b: smallint;
  srcOffset: longword;
  alpha: byte;
  colour: longword;
begin
  if not isImageSet(src) or not isImageSet(dest) then exit;

  srcImage := getImagePtr(src);
  destImage := getImagePtr(dest);

  startX := trunc(max(0, -x));
  startY := trunc(max(0, -y));
  endX := trunc(min(srcImage^.width, destImage^.width - x));
  endY := trunc(min(srcImage^.height, destImage^.height - y));

  for b:=startY to endY - 1 do
  for a:=startX to endX - 1 do begin
    srcOffset := (a + b * srcImage^.width) * 4;
    alpha := srcImage^.dataPtr[srcOffset + 3];
    if alpha < 255 then continue;

    colour := unsafeSprPget(srcImage, a, b);
    unsafeSprPset(destImage, x + a, y + b, colour)
  end;
end;

procedure sprRegionToDest(
  const src, dest: longint;
  const srcX, srcY, srcW, srcH: smallint;
  const destX, destY: smallint);
var
  srcImage, destImage: PImageRef;
  px, py: smallint;
  sx, sy: smallint;
  srcPos: longword;
  alpha: byte;
  colour: longword;
begin
  if not isImageSet(src) or not isImageSet(dest) then exit;

  srcImage := getImagePtr(src);
  destImage := getImagePtr(dest);

  for py:=0 to srcH - 1 do
  for px:=0 to srcW - 1 do begin
    if (destX + px >= destImage^.width) or (destX + px < 0)
      or (destY + py >= destImage^.height) or (destY + py < 0) then continue;

    sx := srcX + px;
    sy := srcY + py;
    srcPos := (sx + sy * srcImage^.width) * 4;

    alpha := srcImage^.dataPtr[srcPos + 3];
    if alpha < 255 then continue;

    colour := unsafeSprPget(srcImage, sx, sy);
    unsafeSprPset(destImage, destX + px, destY + py, colour);
  end;
end;

{ flip: Use SprFlip enum }
procedure sprFlipInPlace(const imgHandle: longint; const flip: smallint);
var
  image: PImageRef;
  px, py: smallint;
  halfW, halfH: smallint;
  tempColour: longword;
  pos1, pos2: longint;
begin
  if flip = SprFlipNone then exit;
  if not isImageSet(imgHandle) then exit;

  image := getImagePtr(imgHandle);

  { Horizontal flip }
  if (flip and SprFlipHorizontal) <> 0 then begin
    halfW := image^.width div 2;

    for py:=0 to image^.height - 1 do
    for px:=0 to halfW - 1 do begin
      pos1 := (px + py * image^.width) * 4;
      pos2 := ((image^.width - 1 - px) + py * image^.width) * 4;

      { Swap RGBA }
      tempColour := PLongword(@image^.dataPtr[pos1])^;
      PLongword(@image^.dataPtr[pos1])^ := PLongword(@image^.dataPtr[pos2])^;
      PLongword(@image^.dataPtr[pos2])^ := tempColour
    end;
  end;

  { Vertical flip }
  if (flip and SprFlipVertical) <> 0 then begin
    halfH := image^.height div 2;

    for py:=0 to halfH - 1 do
    for px:=0 to image^.width - 1 do begin
      pos1 := (px + py * image^.width) * 4;
      pos2 := (px + (image^.height - 1 - py) * image^.width) * 4;

      { Swap RGBA }
      tempColour := PLongword(@image^.dataPtr[pos1])^;
      PLongword(@image^.dataPtr[pos1])^ := PLongword(@image^.dataPtr[pos2])^;
      Plongword(@image^.dataPtr[pos2])^ := tempColour
    end;
  end;
end;


end.
